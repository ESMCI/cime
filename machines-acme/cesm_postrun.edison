#!/bin/csh
# edison cesm_postrun script: 
#  ./cesm_prerun.edison with the following optional argument keyword/value pairs, in any order
#   -case <case name>
#   -jobid <should be same as $PBS_JOBID>
#   -cesmid <$LID in cesm-lingo>
#   -user <should be same as $LOGNAME>
#   -caseroot <directory from which job script was submitted>
#   -rundir <run directory>
#   -save <TRUE|true|anything else>
#   -archive <performance archive root directory>
#   -sampling <sampling interval in seconds>

set case = $PBS_JOBNAME
set app_jobid = $PBS_JOBID
set lid = $PBS_JOBID
set user = $LOGNAME
set caseroot = $PBS_O_WORKDIR
set run_dir = $PBS_O_WORKDIR/run
set save_timing = 'TRUE'
set save_timing_dir = '/project/projectdirs/acme'

set i = 1
while ($i < $#argv)
  if    ("X$argv[$i]" == 'X-case') then
    @ i = $i + 1
    set case = $argv[$i]
  else if ("X$argv[$i]" == 'X-jobid') then
    @ i = $i + 1
    set app_jobid = $argv[$i]
  else if ("X$argv[$i]" == 'X-cesmid') then
    @ i = $i + 1
    set lid = $argv[$i]
  else if ("X$argv[$i]" == 'X-user') then
    @ i = $i + 1
    set user = $argv[$i]
  else if ("X$argv[$i]" == 'X-caseroot') then
    @ i = $i + 1
    set caseroot = $argv[$i]
  else if ("X$argv[$i]" == 'X-rundir') then
    @ i = $i + 1
    set run_dir = $argv[$i]
  else if ("X$argv[$i]" == 'X-save') then
    @ i = $i + 1
    set save_timing = $argv[$i]
  else if ("X$argv[$i]" == 'X-archive') then
    @ i = $i + 1
    set save_timing_dir = $argv[$i]
  endif
  @ i = $i + 1
end

set sdate = `date +"%Y-%m-%d %H:%M:%S"`
echo "aprun ended $sdate" >>& $caseroot/CaseStatus

set syslog_jobid = 0
if (-e $run_dir/syslog_jobid.$app_jobid) then
  set syslog_jobid = `cat $run_dir/syslog_jobid.$app_jobid`
  /bin/rm -f $run_dir/syslog_jobid.$app_jobid
endif
if ($syslog_jobid != 0) then
  kill $syslog_jobid
endif

if (($save_timing == 'TRUE') || ($save_timing == 'true')) then

  if (-d $save_timing_dir) then
    cd $save_timing_dir

    if !(-d performance_archive) then
      mkdir performance_archive
      chmod 777 performance_archive
      chmod +s performance_archive
    endif
    cd performance_archive

    if !(-d $user) then
      mkdir $user
      chmod 775 $user
      chmod +s $user
    endif
    cd $user

    if !(-d $case) then
      mkdir $case
      chmod 775 $case
      chmod +s $case
    endif
    cd $case

    if !(-d $lid) then
      mkdir $lid
      chmod 775 $lid
      chmod +s $lid
    endif

    cd $caseroot
    set string = `ls $app_jobid*OU`
    cp -p $string $save_timing_dir/performance_archive/$user/$case/$lid/$string.$lid
    chmod a+r $save_timing_dir/performance_archive/$user/$case/$lid/$string.$lid
  endif

endif
