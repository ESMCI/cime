#!/bin/csh
# edison cesm_prerun script: 
#  ./cesm_prerun.edison with the following optional argument keyword/value pairs, in any order
#   -machine <system name>
#   -compiler <compiler name>
#   -case <case name>
#   -jobid <should be same as $PBS_JOBID>
#   -cesmid <$LID in cesm-lingo>
#   -user <should be same as $LOGNAME>
#   -caseroot <directory from which job script was submitted>
#   -rundir <run directory>
#   -save <TRUE|true|anything else>
#   -archive <performance archive root directory>
#   -sampling <sampling interval in seconds>

set machine = 'edison'
set compiler = 'intel'
set case = $PBS_JOBNAME
set app_jobid = $PBS_JOBID
set lid = $PBS_JOBID
set user = $LOGNAME
set caseroot = $PBS_O_WORKDIR
set run_dir = $PBS_O_WORKDIR/run
set save_timing = 'TRUE'
set save_timing_dir = '/project/projectdirs/acme'
set sample_interval = 900

set i = 1
while ($i < $#argv)
  if      ("X$argv[$i]" == 'X-machine') then
    @ i = $i + 1
    set machine = $argv[$i]
  else if ("X$argv[$i]" == 'X-compiler') then
    @ i = $i + 1
    set compiler = $argv[$i]
  else if ("X$argv[$i]" == 'X-case') then
    @ i = $i + 1
    set case = $argv[$i]
  else if ("X$argv[$i]" == 'X-jobid') then
    @ i = $i + 1
    set app_jobid = $argv[$i]
  else if ("X$argv[$i]" == 'X-cesmid') then
    @ i = $i + 1
    set lid = $argv[$i]
  else if ("X$argv[$i]" == 'X-user') then
    @ i = $i + 1
    set user = $argv[$i]
  else if ("X$argv[$i]" == 'X-caseroot') then
    @ i = $i + 1
    set caseroot = $argv[$i]
  else if ("X$argv[$i]" == 'X-rundir') then
    @ i = $i + 1
    set run_dir = $argv[$i]
  else if ("X$argv[$i]" == 'X-save') then
    @ i = $i + 1
    set save_timing = $argv[$i]
  else if ("X$argv[$i]" == 'X-archive') then
    @ i = $i + 1
    set save_timing_dir = $argv[$i]
  else if ("X$argv[$i]" == 'X-sampling') then
    @ i = $i + 1
    set sample_interval = $argv[$i]
  endif
  @ i = $i + 1
end

if (-e $run_dir/cesm_prerun_done) then
  /bin/rm $run_dir/cesm_prerun_done
endif

set string = `qstat -f $app_jobid | grep ctime`
echo "job $app_jobid created: $string" >>& $caseroot/CaseStatus

set string = `qstat -f $app_jobid | grep qtime`
echo "job $app_jobid queued: $string" >>& $caseroot/CaseStatus

set string = `qstat -f $app_jobid | grep etime`
echo "job $app_jobid eligible to run: $string" >>& $caseroot/CaseStatus

set string = `qstat -f $app_jobid | grep start_time`
echo "job $app_jobid started: $string" >>& $caseroot/CaseStatus

set syslog_jobid = 0
if (($save_timing == 'TRUE') || ($save_timing == 'true')) then

  if (-d $save_timing_dir) then
    cd $save_timing_dir

    if !(-d performance_archive) then
      mkdir performance_archive
      chmod 777 performance_archive
      chmod +s performance_archive
    endif
    cd performance_archive

    if !(-d $user) then
      mkdir $user
      chmod 775 $user
      chmod +s $user
    endif
    cd $user

    if !(-d $case) then
      mkdir $case
      chmod 775 $case
      chmod +s $case
    endif
    cd $case

    if !(-d $lid) then
      mkdir $lid
      chmod 775 $lid
      chmod +s $lid
    endif
    cd $lid

    if !(-d checkpoints) then
      mkdir checkpoints
      chmod 775 checkpoints
      chmod +s checkpoints
    endif

    xtprocadmin > xtprocadmin.$lid
    qstat -f > qstatf.$lid
    qstat -f $app_jobid > qstatf_jobid.$lid
    xtnodestat > xtnodestat.$lid
    showq > showq.$lid
    chmod a+r *

    gzip xtprocadmin.$lid qstatf.$lid qstatf_jobid.$lid xtnodestat.$lid showq.$lid

    mkdir CaseDocs.$lid
    chmod 775 CaseDocs.$lid
    chmod +s CaseDocs.$lid
    cd CaseDocs.$lid
    cp -p $caseroot/CaseDocs/* .
    cp -p $caseroot/*.run .
    cp -p $caseroot/*.xml .
    cp -p $caseroot/user_nl_* .
    cp -p $caseroot/env_mach_specific .
    cp -p $caseroot/Macros .
    cp -p $caseroot/README.case .
    if (-f $caseroot/Depends.$machine) then
      cp -p $caseroot/Depends.$machine .
    endif
    if (-f $caseroot/Depends.$compiler) then
      cp -p $caseroot/Depends.$compiler .
    endif
    if (-f $caseroot/Depends.$machine.$compiler) then
      cp -p $caseroot/Depends.$machine.$compiler .
    endif
    chmod a+r *

    if ($sample_interval > 0) then
      touch $run_dir/cesm.log.$lid
      cd $caseroot/Tools
      ./mach_syslog $sample_interval $app_jobid $lid $run_dir $run_dir/timing/checkpoints $save_timing_dir/performance_archive/$user/$case/$lid/checkpoints &
      set syslog_jobid = $!
    endif
  endif

endif

cat >> $run_dir/syslog_jobid.$app_jobid << EOF1
$syslog_jobid
EOF1

sleep 10

set sdate = `date +"%Y-%m-%d %H:%M:%S"`
echo "aprun started $sdate" >>& $caseroot/CaseStatus

touch $run_dir/cesm_prerun_done

